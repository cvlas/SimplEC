space 		::= 	spaceChar, space
		|	"".

goal		::= 	space, ceDefinition, space, goal
		|	"".

ceDefinition	::=	initially
		|	durative
		|	instantaneous.

initially	::=	"initially", space, fluent, ".".

durative	::= 	head, space, sep, space, duraBody, ".".

instantaneous	::=	head, space, sep, space, instaBody, ".".

sep		::= 	"iff"
		|	"if".

head		::= 	fluent
		|	"initiate", space, fluent
		|	"terminate", space, fluent
		|	"happens", space, event.

fluent		::= 	functor, "(", argumentsList, ")", value.

event		::=	functor, "(", argumentsList, ")".

functor 	::= 	lower, restChars.

variable	::=	upper, restChars
		|	"_", restChars.

value		::=	"=", argument
		|	"".

argumentsList	::= 	argument, moreArguments.

argument 	::= 	functor
		|	variable
		|	number.

moreArguments	::=	",", space, argument, moreArguments
		|	"".

duraBody	::=	expression.

expression	::=	component, moreComponents.

component	::=	fluent
		|	"(", space, expression, space, ")"
		|	"not", space, expression.

moreComponents	::=	",", space, expression
		|	space, "or", space, expression
		|	"".

instaBody	::=	"happens", space, event, moreConditions
		|	"not happens", space, event, moreConditions
		|	"start", space, fluent, moreConditions
		|	"end", space, fluent, moreConditions.

condition	::=	"start", space, fluent
		|	"end", space, fluent
		|	"happens", space, event
		|	"not happens", space, event
		|	fluent
		|	"not", space, fluent.

moreConditions	::=	",", space, condition, moreConditions
		|	"".

spaceChar	::=	"\t" | "\n" | "\r" | " ".

lower		::=	[a-z].

upper		::=	[A-Z].

number		::= 	[0-9], number
		|	"".

restChars	::= 	[a-zA-Z0-9_], restChars
		|	"".


